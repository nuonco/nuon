---
title: 'Create an AWS ECS App'
description: 'Learn how to create an AWS ECS BYOC application.'
icon: 'aws'
---

This guide walks you through creating a BYOC application using AWS ECS, and deploying it to an install. At the end of this tutorial, you should know how to create a new BYOC deployment offering with your existing application.

## Prerequisites

- [Create a Nuon account](/get-started/quickstart). You will need a working Nuon org.
- [Install Terraform](https://developer.hashicorp.com/terraform/install). You will use our Terraform provider to configure the app.
- [Set up an AWS account](https://docs.aws.amazon.com/SetUp/latest/UserGuide/setup-overview.html). This is the account you will create the install in.

## What You Will Create

This tutorial will walk you through creating the following:

- An App
- A Docker Build component
- A Terraform component
- An Install, using our AWS ECS sandbox

## Application Architecture

When the app is deployed to the install, the following resources will be created in your AWS account.

![ECS Service Architecture Diagram](/images/guides/aws-ecs-app-tutorial.jpg)

## Configure App

In each section below, we will provide you with Terraform you can copy to manage your Nuon app config. Before that, you will need to set up the Terraform project itself. Copy the code below into files with the indicated names.

<Note>
All the configuration and component code used in this guide can be found in our [Guides repo](https://github.com/nuonco/guides/tree/main/create-an-aws-ecs-byoc-app).
</Note>

<CodeGroup>
```sh versions.tf
terraform {
  required_version = ">= 1.3.7"

  required_providers {
    nuon = {
      source  = "nuonco/nuon"
      version = ">= 0.9.1"
    }

    utils = {
      source  = "cloudposse/utils"
      version = ">= 0.17.23"
    }
  }
}
```

```hcl providers.tf
provider "nuon" {}
```
</CodeGroup>

Then run `terraform init`.

### Setup App

Define the app itself and give it a name.

```hcl app.tf
resource "nuon_app" "my_ecs_app" {
  name = "my_ecs_app"
}
```

### Installer

Add an installer. Installers provide an out-of-the-box installation flow your customers can use to install your app.

```hcl installer.tf
resource "nuon_app_installer" "my_ecs_app" {
  app_id            = nuon_app.my_ecs_app.id
  name              = "My ECS App"
  description       = "A demo app that runs on ECS."
  slug              = nuon_app.my_ecs_app.name
  documentation_url = "https://docs.nuon.co/"
  community_url     = "https://join.slack.com/t/nuoncommunity/shared_invite/zt-1q323vw9z-C8ztRP~HfWjZx6AXi50VRA"
  logo_url          = "https://assets-global.website-files.com/62a2c1332b518a9eedc6de2f/651df2030c43865b9b16046b_Group%2048.png"
  github_url        = "https://github.com/nuonco"
  homepage_url      = "https://www.nuon.co/"
  demo_url          = "https://www.nuon.co/"
}
```

### Sandbox

Use our AWS ECS sandbox. It will provide everything you need to run ECS services, from the ECS Fargate cluster down to the VPC.

```hcl sandbox.tf
resource "nuon_app_sandbox" "main" {
  app_id            = nuon_app.my_ecs_app.id
  terraform_version = "v1.6.3"
  public_repo = {
    repo      = "nuonco/sandboxes"
    branch    = "main"
    directory = "aws-ecs"
  }
}
```

### Runner

The `aws-ecs` sandbox requires that we use the `aws-ecs` runner. The runner manages the sandbox, provisioning and deprovisioning AWS resources during deploys.

<Tip>
The `aws-ecs` runner is so named because it runs on ECS, but it can be used to manage any AWS resources. Since it runs on ECS Fargate, no resources beyond what is needed for the runner are provisioned by default. You can use our ECS sandbox and runner to manage Lambda or EC2 deployments without worrying about extraneous ECS costs.
</Tip>

```hcl runner.tf
resource "nuon_app_runner" "main" {
  app_id      = nuon_app.my_ecs_app.id
  runner_type = "aws-ecs"
}
```

### Sync App Config to Nuon

You now have a complete Nuon app config. This is a good place to stop and sync it to Nuon.

```sh
terraform apply
```

Once the config is synced, you should be able to select the newly created app using the CLI.

```sh
nuon apps select
```

All subsequent commands will be run in the context of the new app.

## Connect Components

This app consists of two components: one to build the Docker image, and another to provision the ECS service.

### Docker Image

This is a [Docker Build component](/guides/docker-build-components) that will build the API and create a Docker image containing it. When released, it will sync the image to each install's ECR so ECS can pull it when creating tasks.

<Tip>
Do you still need a Docker Build component if you have a pre-built Docker image? Technically, no. But, ECS Fargate does not cache images, and will pull from the remote source every time it starts a task. In such cases, we recommend using a [Container Image component](/guides/container-image-components).
</Tip>

```hcl docker_image.tf
resource "nuon_docker_build_component" "docker_image" {
  app_id = nuon_app.my_ecs_app.id
  name   = "docker_image"

  public_repo = {
    repo      = "nuonco/guides"
    directory = "create-an-aws-ecs-byoc-app/components/docker-image"
    branch    = "ja/3837-ecs-app-guide"
  }
}
```

### ECS Service

This component will create an ECS service using the docker image, and expose it to the internet with an ALB. It requires some info about the install it will run in, so we use Nuon variables to interpolate the required info on a per-install basis. See our [Using Variables](/guides/using-variables) guide for more info about how this works.

```hcl ecs_service.tf
resource "nuon_terraform_module_component" "ecs_service" {
  app_id = nuon_app.my_ecs_app.id
  name   = "ecs_service"

  public_repo = {
    repo      = "nuonco/guides"
    directory = "create-an-aws-ecs-byoc-app/components/ecs-service"
    branch    = "ja/3837-ecs-app-guide"
  }

  # Service config

  var {
    name  = "cluster_arn"
    value = "{{.nuon.install.sandbox.outputs.ecs_cluster.arn}}"
  }

  var {
    name  = "image_url"
    value = "{{.nuon.components.docker_image.image.repository.uri}}"
  }

  var {
    name  = "image_tag"
    value = "{{.nuon.components.docker_image.image.tag}}"
  }

  var {
    name  = "app_id"
    value = "{{.nuon.app.id}}"
  }

  var {
    name  = "org_id"
    value = "{{.nuon.org.id}}"
  }

  var {
    name  = "install_id"
    value = "{{.nuon.install.id}}"
  }


  # Load balancer config

  var {
    name  = "vpc_id"
    value = "{{.nuon.install.sandbox.outputs.vpc.id}}"
  }

  var {
    name  = "subnet_ids"
    value = "{{.nuon.install.sandbox.outputs.vpc.private_subnet_ids}}"
  }

  var {
    name  = "domain_name"
    value = "electric.{{.nuon.install.sandbox.outputs.public_domain.name}}"
  }

  var {
    name  = "zone_id"
    value = "{{.nuon.install.sandbox.outputs.public_domain.zone_id}}"
  }

  dependencies = [
    nuon_docker_build_component.docker_image.id
  ]
}
```

### Sync Component Configs to Nuon

Run `terraform apply` again to sync the component configs to Nuon. Just like the app, you can use the CLI to verify they were synced successfully.

```sh
nuon components list
```

Initial builds for each component will also have been created. Verify with the CLI that they were successful.

<CodeGroup>
```sh verify docker_image build
nuon builds list -c docker_image
```

```sh verify ecs_service build
nuon builds list -c ecs_service
```
</CodeGroup>

## Create an Install

Creating an install requires two steps: granting access to the AWS account via an IAM role, and then provisioning the sandbox to that account.

### Create an IAM Role

To be able to provision an install in the AWS account, you need to grant Nuon access via an IAM Role. There are a few ways to do this. Follow our [Install Access Permissions](/guides/install-access-permissions) guide, then continue with this tutorial.

### Create an Install

There are a few ways to create an install. Follow our [Create Installs](/guides/create-installs) guide to set one up, then come back.

### Monitor Install Creation

Monitor the install's creation using the CLI.

<CodeGroup>
```sh select the install
nuon installs select
```

```sh list sandbox runs
nuon installs sandbox-runs
```

```sh view sandbox run logs
nuon installs sandbox-run-logs -r {run_id}
```
</CodeGroup>

You will know the install is provisioned when its status is "active".

### Monitor Deploys

When the install is ready, it will check to see if there are any builds available for the app components. If there are, it will create deploys to update itself. You can monitor these with the CLI as well.

<CodeGroup>
```sh list deploys
nuon installs list-deploys
```

```sh view deploy logs
nuon installs deploy-logs -d {deploy_id}
```
</CodeGroup>

You will know the deploys are complete when their statuses are "active".

### Inspect the Install

When the install is created, and the deploys have completed, get the install ID and curl the API to verify it's running.

<CodeGroup>
```sh get install ID
nuon installs list
```

```sh curl api
curl https://api.{install_id}.nuon.run/introspect/env
```
</CodeGroup>

### Update the Install

Now that you have a working install of the app in AWS, you can update it by creating releases.

In `ecs_service.tf`, update the value of the `app_id` var to `updated manually`, and sync it to Nuon.

```sh
terraform apply
```

Then, create a release to update the install. You do not need a new build, since you did not change the component source code, just the config.

```sh
nuon releases create -c ecs_service
```

When the release is active, curl the API to verify the value was updated.

```sh
curl https://api.{install_id}.nuon.run/introspect/env
```

## Wrapping Up and Next Steps

Congratulations, you just deployed an ECS service to AWS! A few suggestions for where to go next:

- Check out our [Release Management](/guides/create-releases) guide to learn how to update installs.
- Dig into our [App Configuration](/guides/apps) guide to learn how to configure more complex apps.
- Share your installer with a friend and have them install your app in their AWS account.
