---
title: 'Quickstart'
description: "Get up and running with Nuon in a few minutes."
---

This guide will walk you through setting up an organization, configuring an application, and deploying to an install. Once you've completed it, you will be ready to configure and deploy your own app.

<Note>
Everything in this guide will be done in sandbox mode to avoid incurring real-world costs. See [Using Sandbox Mode](guides/sandbox-mode) for info.
</Note>

## Install CLI

Refer to [cli installation](./cli.mdx) to install the CLI before proceeding.

## Set up an Organization

### Create an Account

Once you've installed the Nuon CLI, create a Nuon account by logging in.

```sh
nuon login
```

Follow the prompts in the browser and login using a Google account. Once you've logged in you can verify your API token with the `nuon orgs api-token` command. You should also now have a config file at `~/.nuon`. Values saved to this file will be used as CLI flag defaults.

### Create an Organization

<Note>
If you've already created a sandbox org, run `nuon orgs select` instead.
</Note>

Now that you're logged in you can create your first organization in sandbox mode.

<CodeGroup>
```sh create org
nuon orgs create --name {your-org-name} --sandbox-mode
```

```sh view org
nuon orgs current
```
</CodeGroup>

### Connect a GitHub account

<Note>
You don't need to connect to GitHub for this guide, since we're deploying an open-source Docker image. This step is only required to deploy code from a private repo.
</Note>

To deploy code from private repos you'll need to connect to your GitHub account or organization.

<CodeGroup>
```sh connect
nuon orgs connect-github
```

```sh list repos
nuon orgs list-connected-repos
```
</CodeGroup>

## Configure an Application

We'll configure a simple app, consisting of a single container. The recommended way to do this is using our Terraform provider. Set up a new Terraform project with the following code.

<CodeGroup>
```hcl versions.tf
terraform {
  required_version = ">= 1.3.7"

  required_providers {
    nuon = {
      source  = "nuonco/nuon"
      version = ">= 0.9.0"
    }
  }
}

provider "nuon" {}
```

```hcl variables.tf
variable "install_count" {
  type        = number
  description = "number of installs to create and manage with terraform"
}

variable "install_region" {
  type        = string
  description = "region to create installs in"
  default     = "us-west-2"
}

variable "install_iam_role_arn" {
  type        = string
  description = "iam role arn to use for install access"
  default     = ""
}
```

```hcl terraform.tfvars
install_region = "us-west-2"
# we'll update these two later
install_count = 0
install_iam_role_arn = ""
```

```hcl app.tf
resource "nuon_app" "example" {
  name = "quickstart_test_app"
}

resource "nuon_app_runner" "main" {
    app_id = nuon_app.example.id
    runner_type = "aws-eks"
}

resource "nuon_app_sandbox" "main" {
  app_id            = nuon_app.example.id
  terraform_version = "v1.6.3"

  public_repo = {
    repo      = "https://github.com/nuonco/sandboxes"
    branch    = "main"
    directory = "aws-eks"
  }
}
```

```hcl http_bin.tf
resource "nuon_container_image_component" "http_bin" {
  name   = "http_bin"
  app_id = nuon_app.example.id

  public = {
    image_url = "kennethreitz/httpbin"
    tag       = "latest"
  }
}
```

```hcl installs.tf
resource "nuon_install" "customer_one" {
  count = var.install_count

  name         = "quickstart-${count.index}"
  app_id       = nuon_app.example.id
  region       = var.install_region
  iam_role_arn = var.install_iam_role_arn

  depends_on = [
    nuon_app_sandbox.main,
  ]
}
```
</CodeGroup>

Run `terraform init` and `terraform apply` just like you would for any Terraform project. The Nuon provider will use the same config as the CLI to create a new app in your current org.

When it's done, run `nuon apps select` to see the new app ID and save it to the config file.

## Create and Deploy to an Install

Next we'll create some installs to deploy your app to. Typically, installs would be created by customers through a sign-up flow, but you can also create installs yourself via the Terraform provider.

### Set up an Install Role

<Note>
Since we're using a sandbox org, you don't need a real AWS account. You can provide any string as an IAM role ARN, and Nuon will simulate everything. This step is only required for real orgs.
</Note>

First, you'll need to create an IAM role in the target AWS account. This will provide your org runner with access to provision resources. Create a new Terraform project using the following code.

<CodeGroup>
```hcl versions.tf
terraform {
  required_version = ">= 1.3.7"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"
    }
  }
}

provider "aws" {}
```

```hcl variables.tf
variable "app_name" {
  type        = string
  description = "Name of the app to be installed."
}

locals {
  default_tags = {
    app_name = var.app_name
  }

  artifact_base_url = "https://github.com/nuonco/sandboxes/tree/main/aws-eks/artifacts"
}
```

```hcl role.tf
data "http" "sandbox_trust_policy" {
  url = "${local.artifact_base_url}/trust.json"
}

module "install_access" {
  tags        = local.default_tags
  source      = "terraform-aws-modules/iam/aws//modules/iam-assumable-role"
  version     = ">= 5.1.0"
  create_role = true
  role_name   = "${var.app_name}-install-access"

  allow_self_assume_role          = true
  create_custom_role_trust_policy = true
  custom_role_trust_policy        = data.http.sandbox_trust_policy.response_body
  custom_role_policy_arns = [
    aws_iam_policy.deprovision.arn,
    aws_iam_policy.provision.arn
  ]
}
```

```hcl policies.tf
data "http" "sandbox_provision_policy" {
  url = "${local.artifact_base_url}/provision.json"
}

resource "aws_iam_policy" "provision" {
  tags        = local.default_tags
  name        = "${var.app_name}-provision"
  path        = "/"
  description = "Provides access to provision a new install of ${var.app_name}"
  policy      = data.http.sandbox_provision_policy.response_body
}

data "http" "sandbox_deprovision_policy" {
  url = "${local.artifact_base_url}/deprovision.json"
}

resource "aws_iam_policy" "deprovision" {
  tags        = local.default_tags
  name        = "${var.app_name}-deprovision"
  path        = "/"
  description = "Provides access to deprovision an install  of ${var.app_name}"
  policy      = data.http.sandbox_deprovision_policy.response_body
}
```
</CodeGroup>

Run `terraform init` and `terraform apply`. Copy the ARN of the newly-created install-access role.

### Provisioning the Sandbox

Go back to your app config Terraform project, update the value of `install_iam_role_arn` to the ARN of the IAM role you created, and set `install_count` to `1`. Run `terraform apply` to provision the sandbox to your install.

<Note>
Since you're using a sandbox org, you can create as many installs as you want, using whatever string you want as the ARN for all of them.
</Note>

### Deploying to your Install

Once an install has been created, you can deploy your app to it.

```sh create release
nuon releases create -c http_bin --auto-build
```

This will create a new build of your component and release it. The release will create a deploy for each install.

<Note>
See [Creating a Release](./guides/create-releases) for more info on working with releases.
</Note>

## Next Steps

Now that you know the basics, the next step is to create a real org, configure your own app, and deploy it to real installs.

- Create an org without the `--sandbox-mode` flag to provision real resources.
- Connect that org to GitHub to pull in your own source code.
- Set up an IAM role in a real AWS account to provision a real install.
